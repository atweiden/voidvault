#!/usr/bin/env raku
use v6;
use Voidvault::Parser::VaultOffset;
use X::Voidvault::Parser::VaultOffset;

constant $HELP = q:to/EOF/.trim;
Usage:
  gen-cryptsetup-luks-offset [--sector-size=<bytes>] <offset>
  gen-cryptsetup-luks-offset 5G
  cryptsetup --offset $(gen-cryptsetup-luks-offset 5GiB) luksFormat /dev/sda

Options:
  -h, --help
    Print this help message
  --sector-size=<bytes>
    Sector size for use with LUKS encrypted volume

Offset
  K,KiB     kibibytes
  M,MiB     mebibytes
  G,GiB     gibibtes
  T,TiB     tebibytes

Sector Size
  If specified, value must range from 512 to 4096 and be power of 2
  If unspecified, value defaults to physical block (sector) size of
  partition root directory is located on
EOF

sub MAIN(Str:D $content, *%opts (UInt :sector-size($)) --> Nil)
{
    my $offset = try Voidvault::Parser::VaultOffset.parse($content, |%opts);
    bail($!.message) if $!;
    say($offset);
}

sub bail(Str:D $message)
{
    note($message);
    note('');
    USAGE(:error);
    exit(1);
}

multi sub USAGE(Bool:D :error($)! where .so --> Nil)
{
    note($HELP);
}

multi sub USAGE(--> Nil)
{
    say($HELP);
}

# vim: set filetype=raku foldmethod=marker foldlevel=0:
