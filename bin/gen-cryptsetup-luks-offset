#!/usr/bin/env raku
use v6;
use Voidvault::Parser::VaultOffset;
use X::Voidvault::Parser::VaultOffset;

constant $HELP = q:to/EOF/.trim;
Usage:
  gen-cryptsetup-luks-offset [--json] [--sector-size=<bytes>] <offset>
  gen-cryptsetup-luks-offset 5G
  offset="$(gen-cryptsetup-luks-offset --json 5GiB | jq '."offset"')"
  cryptsetup --offset "$offset" luksFormat /dev/sda

Options:
  -h, --help
    Print this help message
  --json
    Output JSON
  --sector-size=<bytes>
    Sector size for use with LUKS encrypted volume

Offset
  K,KiB     kibibytes
  M,MiB     mebibytes
  G,GiB     gibibtes
  T,TiB     tebibytes

Sector Size
  If specified, value must range from 512 to 4096 and be power of 2
  If unspecified, value defaults to physical block (sector) size of
  partition root directory is located on
EOF

sub MAIN(Str:D $content, Bool :$json, *%opts (UInt :sector-size($)) --> Nil)
{
    my %offset-sector-size =
        try Voidvault::Parser::VaultOffset.parse($content, |%opts);
    bail($!.message) if $!;
    my Str:D $output =
        $json ?? Rakudo::Internals::JSON.to-json(%offset-sector-size)
              !! sprintf("%d sector offset, %d byte sector size",
                         %offset-sector-size<offset>,
                         %offset-sector-size<sector-size>);
    say($output);
}

sub bail(Str:D $message)
{
    note($message);
    note('');
    USAGE(:error);
    exit(1);
}

multi sub USAGE(Bool:D :error($)! where .so --> Nil)
{
    note($HELP);
}

multi sub USAGE(--> Nil)
{
    say($HELP);
}

# vim: set filetype=raku foldmethod=marker foldlevel=0:
