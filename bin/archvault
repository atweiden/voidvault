#!/usr/bin/env perl6




use v6;
use Archvault;
use Archvault::Utils;




# -----------------------------------------------------------------------------
# disable-cow
# -----------------------------------------------------------------------------

multi sub MAIN(
    'disable-cow',
    Bool :c(:$clean),
    Str :g(:$group),
    Str :p(:$permissions),
    Bool :r(:$recursive),
    Str :u(:$user),
    *@directory
    --> Nil
)
{
    my %opts;
    %opts<clean> = $clean if $clean;
    %opts<group> = $group if $group;
    %opts<permissions> = $permissions if $permissions;
    %opts<recursive> = $recursive if $recursive;
    %opts<user> = $user if $user;
    Archvault::Utils.disable-cow(@directory, |%opts);
}




# -----------------------------------------------------------------------------
# gen-pass-hash
# -----------------------------------------------------------------------------

multi sub MAIN('gen-pass-hash', Str:D :p(:$pass)!, Bool :g(:$grub) --> Nil)
{
    say(Archvault::Utils.gen-pass-hash($pass, :$grub));
}

multi sub MAIN('gen-pass-hash', Bool :g(:$grub) --> Nil)
{
    say(Archvault::Utils.prompt-pass-hash(:$grub));
}




# -----------------------------------------------------------------------------
# help
# -----------------------------------------------------------------------------

multi sub MAIN('help', 'disable-cow' --> Nil)
{
    USAGE('disable-cow');
}

multi sub MAIN('help', 'gen-pass-hash' --> Nil)
{
    USAGE('gen-pass-hash');
}

multi sub MAIN('help', 'ls' --> Nil)
{
    USAGE('ls');
}

multi sub MAIN('help', 'new' --> Nil)
{
    USAGE('new');
}

multi sub MAIN('help', $? --> Nil)
{
    USAGE();
}




# -----------------------------------------------------------------------------
# new
# -----------------------------------------------------------------------------

multi sub MAIN(
    'new',
    *%opts (
        Str :admin-name($),
        Str :admin-pass($),
        Str :admin-pass-hash($),
        Bool :augment($),
        Str :disk-type($),
        Str :graphics($),
        Str :grub-name($),
        Str :grub-pass($),
        Str :grub-pass-hash($),
        Str :guest-name($),
        Str :guest-pass($),
        Str :guest-pass-hash($),
        Str :hostname($),
        Str :keymap($),
        Str :locale($),
        Str :partition($),
        Str :processor($),
        Bool :reflector($),
        Str :root-pass($),
        Str :root-pass-hash($),
        Str :sftp-name($),
        Str :sftp-pass($),
        Str :sftp-pass-hash($),
        Str :timezone($),
        Str :vault-name($),
        Str :vault-pass($)
    )
    --> Nil
)
{
    Archvault.new(|%opts);
}




# -----------------------------------------------------------------------------
# ls
# -----------------------------------------------------------------------------

multi sub MAIN('ls', 'keymaps' --> Nil)
{
    Archvault::Utils.ls-keymaps.map({ .say });
}

multi sub MAIN('ls', 'locales' --> Nil)
{
    Archvault::Utils.ls-locales.map({ .say });
}

multi sub MAIN('ls', 'partitions' --> Nil)
{
    Archvault::Utils.ls-partitions.map({ .say });
}

multi sub MAIN('ls', 'timezones' --> Nil)
{
    Archvault::Utils.ls-timezones.map({ .say });
}




# -----------------------------------------------------------------------------
# version
# -----------------------------------------------------------------------------

multi sub MAIN(Bool:D :version($)! where .so --> Nil)
{
    printf("v%s\n", $Archvault::VERSION);
    exit(0);
}




# -----------------------------------------------------------------------------
# usage
# -----------------------------------------------------------------------------

multi sub USAGE(--> Nil)
{
    constant $HELP = q:to/EOF/.trim;
    Usage:
      archvault <command>

    Commands:
      disable-cow          Disable copy-on-write on directories
      gen-pass-hash        Generate password hash
      help                 Show help for subcommands
      ls                   List keymaps, locales, partitions, timezones
      new                  Bootstrap Arch system with FDE

    Options:
      -h, --help
        Print this help message
      --version
        Print version and exit
    EOF
    say($HELP);
}

multi sub USAGE('disable-cow' --> Nil)
{
    constant $HELP = qq:to/EOF/.trim;
    Usage:
      archvault [--clean]
                [--permissions=<octal>]
                [--user=<username>]
                [--group=<groupname>]
                disable-cow <directory> [<directory> <directory>..]
      archvault [-r] disable-cow <directory> [<directory> <directory>..]

    Options:
      -c, --clean
        Remake directories with CoW disabled
      -r, --recursive
        Recursively disable CoW on directories
      -p, --permissions=<octal>
        Octal file mode bits (defaults to "755")
      -u, --user=<username>
        Named user to own file (defaults to "$*USER")
      -g, --group=<groupname>
        Named group to own file (defaults to "$*GROUP")

    Positionals:
      <directory>        Path to directory
    EOF
    say($HELP);
}

multi sub USAGE('gen-pass-hash' --> Nil)
{
    constant $HELP = q:to/EOF/.trim;
    Usage:
      archvault [-g|--grub] [-p|--pass=<password>] gen-pass-hash

    Options:
      -g, --grub
        Generate PBKDF2 password hash for Grub
      -p, --pass=<password>
        Plaintext password to generate hash from
    EOF
    say($HELP);
}

multi sub USAGE('ls' --> Nil)
{
    constant $HELP = q:to/EOF/.trim;
    Usage:
      archvault ls <positional>

    Positionals
      keymaps           List available keyboard table descriptions
      locales           List available sets of language/cultural rules
      partitions        List available device paths
      timezones         List available geographic regions
    EOF
    say($HELP);
}

multi sub USAGE('new' --> Nil)
{
    constant $HELP = q:to/EOF/.trim;
    Usage:
      archvault [--admin-name=<username>] [--admin-pass=<password>]
                [--guest-name=<username>] [--guest-pass=<password>]
                [--sftp-name=<username>] [--sftp-pass=<password>]
                [--grub-name=<username>] [--grub-pass=<password>]
                [--root-pass=<password>]
                [--vault-name=<vaultname>] [--vault-pass=<password>]
                [--hostname=<hostname>] [--partition=<device>]
                [--processor=<processor>] [--graphics=<graphics>]
                [--disk-type=<disktype>] [--locale=<locale>]
                [--keymap=<keymap>] [--timezone=<timezone>]
                [--augment]
                new

    Options:
      --admin-name=<username>
        User name for admin account
      --admin-pass=<password>
        Password for admin account
      --admin-pass-hash=<passhash>
        Encrypted password hash for admin account
      --augment
        Drop to Bash console mid-execution
      --disk-type=<disktype>
        Hard drive type
      --graphics=<graphics>
        Graphics card type
      --grub-name=<username>
        User name for Grub
      --grub-pass=<password>
        Password for Grub
      --grub-pass-hash=<passhash>
        Password hash for Grub
      --guest-name=<username>
        User name for guest account
      --guest-pass=<password>
        Password for guest account
      --guest-pass-hash=<passhash>
        Encrypted password hash for guest account
      --hostname=<hostname>
        Hostname
      --keymap=<keymap>
        Keymap
      --locale=<locale>
        Locale
      --partition=<device>
        Partition target for install
      --processor=<processor>
        Processor type
      --reflector
        Optimize Pacman mirrors
      --root-pass=<password>
        Password for root account
      --root-pass-hash=<passhash>
        Encrypted password hash for root account
      --sftp-name=<username>
        User name for SFTP account
      --sftp-pass=<password>
        Password for SFTP account
      --sftp-pass-hash=<passhash>
        Encrypted password hash for SFTP account
      --timezone=<timezone>
        Timezone
      --vault-name=<vaultname>
        Name for LUKS encrypted volume
      --vault-pass=<password>
        Password for LUKS encrypted volume
    EOF
    say($HELP);
}

# vim: set filetype=perl6 foldmethod=marker foldlevel=0:
